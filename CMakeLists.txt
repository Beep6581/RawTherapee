cmake_minimum_required(VERSION 2.6)

set (CMAKE_BUILD_TYPE Debug CACHE STRING "One of: None Debug Release RelWithDebInfo MinSizeRel.")
set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}")

if (APPLE)
#    SET (CMAKE_OSX_ARCHITECTURES "i386;x86_64;" )
#    SET (CMAKE_TRY_COMPILE_OSX_ARCHITECTURES "i386;x86_64;" )
    SET (CMAKE_OSX_SYSROOT "/Developer/SDKs/MacOSX10.5.sdk")
    SET (CMAKE_OSX_DEPLOYMENT_TARGET "10.5")
endif (APPLE)

option (BUILD_SHARED "Build rawtherapee with shared libraries" OFF)
option (OPTION_OMP "Build with OpenMP support" ON)

# set install directories
if (NOT DEFINED DATADIR)
    if (WIN32 OR APPLE)
        set (DATADIR ${CMAKE_CURRENT_SOURCE_DIR}/release)
    else (WIN32 OR APPLE)
        set (DATADIR ${CMAKE_INSTALL_PREFIX}/share/rawtherapee)
    endif (WIN32 OR APPLE)
endif (NOT DEFINED DATADIR)

if (NOT DEFINED BINDIR)
    if (WIN32 OR APPLE)
        set (BINDIR  ${CMAKE_CURRENT_SOURCE_DIR}/release)
    else (WIN32 OR APPLE)
        set (BINDIR  ${CMAKE_INSTALL_PREFIX}/bin)
    endif (WIN32 OR APPLE)
endif (NOT DEFINED BINDIR)

if (NOT DEFINED LIBDIR)
    if (WIN32 OR APPLE)
        set (LIBDIR ${CMAKE_CURRENT_SOURCE_DIR}/release)
    else (WIN32 OR APPLE)
        set (LIBDIR ${CMAKE_INSTALL_PREFIX}/lib)
    endif (WIN32 OR APPLE)
endif (NOT DEFINED LIBDIR)

# check for libraries
find_package(PkgConfig)
find_package(FreeImage)
pkg_check_modules (GTK     REQUIRED gtk+-2.0>=2.12)
pkg_check_modules (GLIB2   REQUIRED glib-2.0>=2.16)
pkg_check_modules (GLIBMM  REQUIRED glibmm-2.4>=2.16)
pkg_check_modules (GTKMM   REQUIRED gtkmm-2.4>=2.12)
pkg_check_modules (GIO     REQUIRED gio-2.0>=2.16)
pkg_check_modules (GIOMM   REQUIRED giomm-2.4>=2.12)
pkg_check_modules (CAIROMM REQUIRED cairomm-1.0>=1.8)
pkg_check_modules (GTHREAD REQUIRED gthread-2.0>=2.16)
pkg_check_modules (GOBJECT REQUIRED gobject-2.0>=2.16)
pkg_check_modules (SIGC    REQUIRED sigc++-2.0)
pkg_check_modules (EXIV2   REQUIRED exiv2>=0.21)
pkg_check_modules (LIBRAW  REQUIRED libraw_r>=0.13)

# NOTE: dependencies should be handled by pkg_check_modules and FIND_PACKAGE
# on windows too but I don't want to break current build chain
if (WIN32)
    set (EXTRA_LIBDIR "${CMAKE_CURRENT_SOURCE_DIR}/lib")
    set (EXTRA_INCDIR "${CMAKE_CURRENT_SOURCE_DIR}/winclude")
    set (EXTRA_LIB "ws2_32")
    set (IPTCDATA_LIBRARIES iptcdata)
    set (LCMS_LIBRARIES liblcms.a)
    set (JPEG_LIBRARIES libjpeg.a)
    set (PNG_LIBRARIES libpng.a)
    set (TIFF_LIBRARIES libtiff.a)
    set (ZLIB_LIBRARIES libz.a)
else (WIN32)
#    pkg_check_modules (IPTCDATA REQUIRED libiptcdata)
    pkg_check_modules (LCMS    REQUIRED lcms2)
    find_package (JPEG REQUIRED)
    find_package (PNG REQUIRED)
    find_package (TIFF REQUIRED)
    find_package (ZLIB REQUIRED)
endif (WIN32)

if (OPTION_OMP)
	find_package(OpenMP)
	if (OPENMP_FOUND)
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
	endif (OPENMP_FOUND)
endif (OPTION_OMP)

if (UNIX)
    install (PROGRAMS rtstart DESTINATION ${BINDIR})
endif (UNIX)

add_subdirectory (rtengine)
add_subdirectory (rtgui)
add_subdirectory (rtdata)
