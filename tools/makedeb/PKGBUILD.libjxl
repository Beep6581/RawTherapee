# Maintainer: RawTherapee Developers

_pkgname="libjxl"
pkgname="$_pkgname"
pkgver='0.10.3'
pkgrel='1'
pkgdesc='JPEG XL image format reference implementation'
url='https://github.com/libjxl/libjxl'
license=('BSD')
arch=('amd64')

depends=('libgif-dev' 'libjpeg-dev' 'libopenexr-dev' 'libpng-dev' 'libwebp-dev')
makedepends=('cmake' 'pkg-config' 'libbrotli-dev' 'libgflags-dev')

_pkgsrc="$_pkgname"
source=("git+$url.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cd "$_pkgsrc"
  git submodule update --init --recursive --depth 1 --recommend-shallow
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DJPEGXL_FORCE_SYSTEM_BROTLI=ON
    -DBUILD_TESTING=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build --parallel
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
